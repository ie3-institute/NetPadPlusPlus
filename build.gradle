import com.github.jengelman.gradle.plugins.shadow.transformers.AppendingTransformer

plugins {
	id 'java'
	id 'groovy'
	id 'maven-publish'
	id 'application'
	id 'signing'
	id 'pmd' //code check, working on source code
	id "com.github.spotbugs" version "4.7.3" //code check, working on byte code
	id "com.diffplug.spotless" version "5.14.2" //code format
	id 'com.github.onslip.gradle-one-jar' version '1.0.6' // pack a self contained jar
	id 'org.openjfx.javafxplugin' version '0.0.10' // java 11 javafx plugin for gradle
	id 'jacoco' // java code coverage plugin
	id "org.sonarqube" version "3.3" // sonarqube
	id "com.github.johnrengelman.shadow" version "7.0.0" // fat jar
}

ext {
	//version (changing these should be considered thoroughly!)
	javaVersion = JavaVersion.VERSION_11

	javaFxVersion = '15-ea+1'
	slf4jVersion = '1.7.32'

	scriptsLocation = 'gradle' + File.separator + 'scripts' + File.separator //location of script plugins
}

group = 'com.github.ie3-institute'
description = 'NetPad++'
version = '0.2-SNAPSHOT'

mainClassName = 'edu.ie3.netpad.main.IntelliJMainLauncher'

sourceCompatibility = javaVersion
targetCompatibility = javaVersion

apply from: scriptsLocation + 'tests.gradle'
apply from: scriptsLocation + 'pmd.gradle'
apply from: scriptsLocation + 'spotbugs.gradle'
apply from: scriptsLocation + 'spotless.gradle'
apply from: scriptsLocation + 'checkJavaVersion.gradle'
apply from: scriptsLocation + 'sonarqube.gradle'
apply from: scriptsLocation + 'jacoco.gradle'
apply from: scriptsLocation + 'mavenCentralPublish.gradle'

repositories {
	mavenCentral()
	maven { url "https://jitpack.io" } // allows for github repos as dependencies
	maven { url 'https://oss.sonatype.org/service/local/repositories/snapshots/content' } // snapshot artifacts
}

dependencies {
	// ie³ github repository
	implementation ('com.github.ie3-institute:PowerSystemDataModel:2.0.1') {
		/* Exclude nested logging and ie³ related dependencies */
		exclude group: 'org.slf4j'
		exclude group: 'com.github.ie3-institute'
	}

	implementation ('com.github.ie3-institute:PowerSystemUtils:1.5.3') {
		/* Exclude nested logging and ie³ related dependencies */
		exclude group: 'org.slf4j'
		exclude group: 'com.github.ie3-institute'
	}

	// gluon hq
	implementation 'com.gluonhq:maps:2.0.0-ea+3'
	implementation 'com.gluonhq.attach:util:4.0.12'
	implementation 'com.gluonhq.attach:storage:4.0.7:desktop'

	// JGraphT
	implementation 'org.jgrapht:jgrapht-core:1.5.1'
	implementation 'org.jgrapht:jgrapht-ext:1.5.1'


	// javafx
	implementation "org.openjfx:javafx-fxml:$javaFxVersion"
	implementation "org.openjfx:javafx-swing:$javaFxVersion"
	implementation "org.openjfx:javafx-graphics:$javaFxVersion"
	implementation "org.openjfx:javafx-web:$javaFxVersion"
	implementation 'org.controlsfx:controlsfx:11.1.0'

	// Apache POI
	implementation 'org.apache.poi:poi-ooxml:5.0.0'

	// rx java
	implementation 'io.reactivex:rxjava:1.3.8'

	// logging
	implementation "org.slf4j:slf4j-api:${slf4jVersion}" // slf4j wrapper
	implementation 'com.lmax:disruptor:3.4.4' //async logging
	implementation 'org.apache.logging.log4j:log4j-api:2.14.1' // log4j
	implementation 'org.apache.logging.log4j:log4j-core:2.14.1' // log4j
	implementation 'org.apache.logging.log4j:log4j-slf4j-impl:2.14.1' // log4j -> slf4j


	// testing
	testImplementation 'junit:junit:4.13.2'
	testImplementation 'org.spockframework:spock-core:2.0-groovy-3.0'
	testImplementation 'cglib:cglib-nodep:3.3.0' //enables mocking of classes (in addition to interfaces)
	implementation 'org.mockito:mockito-core:3.11.2' //mocking framework

	// pbf parser //
	implementation 'com.github.johanneshiry:OSMonaut:v1.1.1'
}

wrapper {
	gradleVersion = '7.2'
}

javafx {
	version = javaFxVersion
	// corresponding javafx modules
	modules = [
		'javafx.controls',
		'javafx.fxml',
		'javafx.swing',
		'javafx.graphics',
		'javafx.web'
	]
}

tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

task printVersion {
	doLast {
		println project.version
	}
}

shadowJar {

	transform(AppendingTransformer) {
		resource = 'reference.conf'
	}
	zip64 = true
}
